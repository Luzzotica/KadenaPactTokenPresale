(load "swag-token-env.repl")


(begin-tx "Test governance")
(use free.swag-token)

(env-keys [])
(expect-failure "Can't acquire GOV cap without gov key"
  "Keyset failure (keys-all): 'free.swag-token-gov"
  (test-capability (GOV))
)
(env-keys ["test"])
(expect-failure "Can't acquire GOV cap without gov key"
  "Keyset failure (keys-all): 'free.swag-token-gov"
  (test-capability (GOV))
)

(env-keys ["gov"])
(expect-that "Can acquire GOV cap with gov key"
  (= "Capability acquired")
  (test-capability (GOV))
)

(commit-tx)



(begin-tx "Test init and tokenomics")
(use free.swag-token)

(expect-that "ROOT has tokens"
  (= 800.0)
  (get-balance "ROOT")
)
(expect-that "LIQUIDITY has tokens"
  (= 200.0)
  (get-balance "LIQUIDITY")
)

(env-keys ["gov"])
(env-data {
  "init-data": { 
    "initial-supply": 1000.0, 
    "accounts":[ 
      { 
        "account": "ROOT", 
        "guard": {
          "keys": ["gov"], "pred": "keys-all"
        },
        "percent": 0.8
      },
      { 
        "account": "LIQUIDITY", 
        "guard": {
          "keys": ["gov"], "pred": "keys-all"
        },
        "percent": 0.3
      }
    ]
  }
})
(expect-failure "Percent must add up to 100%"
  "Percentages must add up to 100%"
  (init-token (read-msg "init-data"))
)

(env-keys ["test"])
(expect-failure "Must have permissions to init token"
  "Keyset failure (keys-all): 'free.swag-token-gov"
  (init-token (read-msg "init-data"))
)

(commit-tx)